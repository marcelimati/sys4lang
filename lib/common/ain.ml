open Base

let sprintf = Printf.sprintf

module In_channel = Stdio.In_channel
module Out_channel = Stdio.Out_channel
module Dynarray = Stdlib.Dynarray

module Type = struct
  type t =
    | Void
    | Int
    | Float
    | String
    | Struct of int
    | IMainSystem
    | FuncType of int
    | Bool
    | LongInt
    | Delegate of int
    | HLLFunc2
    | HLLParam
    | Array of t
    | Ref of t
    | Wrap of t
    | Option of t
    | Unknown87 of t
    | IFace of int
    | Enum2 of int
    | Enum of int
    | HLLFunc
    (* only used in delegates, type name has "T" prefix (e.g., TValueType, TResult) *)
    | Unknown98
    | IFaceWrap of int
    (* internal compiler use *)
    | Function
    | Method
    | NullType

  let rec int_of_data_type version o =
    match o with
    | Ref Void -> failwith "tried to create ref void"
    | Ref Int -> 18
    | Ref Float -> 19
    | Ref String -> 20
    | Ref (Struct _) -> 21
    | Ref IMainSystem -> failwith "tried to create ref IMainSystem"
    | Ref (FuncType _) -> 31
    | Ref Bool -> 51
    | Ref LongInt -> 59
    | Ref (Delegate _) -> 67
    | Ref HLLFunc2 -> failwith "tried to create ref hll_func2" (* FIXME: 73? *)
    | Ref HLLParam -> 75
    | Ref (Array arrtype) -> (
        if version >= 11 then 80
        else
          match arrtype with
          | Void -> failwith "tried to create ref array<void>"
          | Int -> 22
          | Float -> 23
          | String -> 24
          | Struct _ -> 25
          | IMainSystem -> failwith "tried to create ref array<IMainSystem>"
          | FuncType _ -> 32
          | Bool -> 52
          | LongInt -> 60
          | Delegate _ -> 69
          | HLLFunc2 -> failwith "tried to create ref array<hll_func2>"
          | HLLParam -> failwith "tried to create ref array<hll_param>"
          | Array t -> int_of_data_type version (Ref (Array t))
          | Ref _ -> failwith "tried to create ref array<ref<...>>"
          | Wrap _ -> failwith "tried to create ref array<wrap<...>>"
          | Option _ -> failwith "tried to create ref array<option<...>>"
          | Unknown87 _ -> failwith "tried to create ref array<unknown_87>"
          | IFace _ -> failwith "tried to create ref array<interface>"
          | Enum2 _ -> failwith "tried to create ref array<enum2>"
          | Enum _ -> failwith "tried to create ref array<enum>"
          | HLLFunc -> failwith "tried to create ref array<hll_func>"
          | Unknown98 -> failwith "tried to create ref array<unknown_98>"
          | IFaceWrap _ -> failwith "tried to create ref array<iface_wrap<...>>"
          | Function -> failwith "tried to create ref array<function>"
          | Method -> failwith "tried to create ref array<method>"
          | NullType -> failwith "tried to create ref array<null>")
    | Ref (Wrap _) -> failwith "tried to create ref wrap<...>"
    | Ref (Option _) -> failwith "tried to create ref option<...>"
    | Ref (Unknown87 _) -> failwith "tried to create ref unknown_87"
    | Ref (IFace _) -> failwith "tried to create ref interface"
    | Ref (Enum2 _) -> failwith "tried to create ref enum2"
    | Ref (Enum _) -> 93
    | Ref HLLFunc -> failwith "tried to create ref hll_func"
    | Ref Unknown98 -> failwith "tried to create ref unknown_98"
    | Ref (IFaceWrap _) -> failwith "tried to create ref iface_wrap<...>"
    | Ref Function -> failwith "tried to create ref function"
    | Ref Method -> failwith "tried to create ref method"
    | Ref NullType -> failwith "tried to create ref null"
    | Ref (Ref _) -> failwith "tried to create ref ref"
    | Void -> 0
    | Int -> 10
    | Float -> 11
    | String -> 12
    | Struct _ -> 13
    | IMainSystem -> 26
    | FuncType _ -> 27
    | Bool -> 47
    | LongInt -> 55
    | Delegate _ -> 63
    | HLLFunc2 -> 71
    | HLLParam -> 74
    | Array arrtype -> (
        if version >= 11 then 79
        else
          match arrtype with
          | Void -> failwith "tried to create array<void>"
          | Int -> 14
          | Float -> 15
          | String -> 16
          | Struct _ -> 17
          | IMainSystem -> failwith "tried to create array<IMainSystem>"
          | FuncType _ -> 30
          | Bool -> 50
          | LongInt -> 58
          | Delegate _ -> 66
          | HLLFunc2 -> failwith "tried to create array<hll_func2>"
          | HLLParam -> failwith "tried to create array<hll_param>"
          | Array t -> int_of_data_type version (Array t)
          | Ref _ -> failwith "tried to create array<ref<...>>"
          | Wrap _ -> failwith "tried to create array<wrap<...>>"
          | Option _ -> failwith "tried to create array<option<...>>"
          | Unknown87 _ -> failwith "tried to create array<unknown_87>"
          | IFace _ -> failwith "tried to create array<interface>"
          | Enum2 _ -> failwith "tried to create array<enum2>"
          | Enum _ -> failwith "tried to create array<enum>"
          | HLLFunc -> failwith "tried to create array<hll_func>"
          | Unknown98 -> failwith "tried to create array<unknown_98>"
          | IFaceWrap _ -> failwith "tried to create array<iface_wrap<...>>"
          | Function -> failwith "tried to create array<function>"
          | Method -> failwith "tried to create array<method>"
          | NullType -> failwith "tried to create array<null>")
    | Wrap _ -> 82
    | Option _ -> 86
    | Unknown87 _ -> 87
    | IFace _ -> 89
    | Enum2 _ -> 91
    | Enum _ -> 92
    | HLLFunc -> 95
    | Unknown98 -> 98
    | IFaceWrap _ -> 100
    | Function -> failwith "tried to create function"
    | Method -> failwith "tried to create method"
    | NullType -> failwith "tried to create null"

  let rec int_of_struct_type ?(var = false) version o =
    match o with
    | Struct no | Enum2 no | Enum no | IFace no | IFaceWrap no -> no
    | Delegate no | FuncType no -> if version < 11 then -1 else no
    | Array t -> (
        (* XXX: preserve quirk with enum struct type in ain v12 (Rance 10) *)
        match t with
        | Enum _ ->
            if version = 12 then -1 else int_of_struct_type version t ~var
        | _ -> int_of_struct_type version t ~var)
    | Ref t | Wrap t | Option t | Unknown87 t ->
        int_of_struct_type version t ~var
    | Void -> if var && version < 11 then 0 else -1
    | _ -> -1

  let rec int_of_rank version o =
    match (version >= 11, o) with
    | _, Ref t -> int_of_rank version t
    | false, Array t -> 1 + int_of_rank version t
    | true, Array _ -> 1
    | _, (Wrap _ | Option _ | Unknown87 _) -> 1
    | _ -> 0

  (* temporary representation *)
  type parsed = { data : int; struc : int; rank : int; subtype : parsed option }

  let is_ref = function Ref _ -> true | _ -> false

  let is_scalar = function
    | Int | Bool | Float | LongInt | FuncType _ -> true
    | _ -> false

  let rec of_parsed parsed =
    (* constructor for old array types *)
    let rec make_array elem_t rank =
      if rank = 0 then elem_t else Array (make_array elem_t (rank - 1))
    in
    let struc = parsed.struc in
    let rank = parsed.rank in
    let unwrap_subtype = function Some st -> of_parsed st | None -> Void in
    match parsed.data with
    | 0 -> Void
    | 10 -> Int
    | 11 -> Float
    | 12 -> String
    | 13 -> Struct struc
    | 14 -> make_array Int rank
    | 15 -> make_array Float rank
    | 16 -> make_array String rank
    | 17 -> make_array (Struct struc) rank
    | 18 -> Ref Int
    | 19 -> Ref Float
    | 20 -> Ref String
    | 21 -> Ref (Struct struc)
    | 22 -> Ref (make_array Int rank)
    | 23 -> Ref (make_array Float rank)
    | 24 -> Ref (make_array String rank)
    | 25 -> Ref (make_array (Struct struc) rank)
    | 26 -> IMainSystem
    | 27 -> FuncType struc
    | 30 -> make_array (FuncType struc) rank
    | 31 -> Ref (FuncType struc)
    | 32 -> Ref (make_array (FuncType struc) rank)
    | 47 -> Bool
    | 50 -> make_array Bool rank
    | 51 -> Ref Bool
    | 52 -> Ref (make_array Bool rank)
    | 55 -> LongInt
    | 58 -> make_array LongInt rank
    | 59 -> Ref LongInt
    | 60 -> Ref (make_array LongInt rank)
    | 63 -> Delegate struc
    | 66 -> make_array (Delegate struc) rank
    | 67 -> Ref (Delegate struc)
    | 69 -> Ref (make_array (Delegate struc) rank)
    | 71 -> HLLFunc2
    | 74 -> HLLParam
    | 75 -> Ref HLLParam
    (* XXX: HLL function can have null subtype *)
    | 79 -> Array (unwrap_subtype parsed.subtype)
    | 80 -> Ref (Array (unwrap_subtype parsed.subtype))
    | 82 -> Wrap (unwrap_subtype parsed.subtype)
    | 86 -> Option (unwrap_subtype parsed.subtype)
    | 87 -> Unknown87 (unwrap_subtype parsed.subtype)
    | 89 -> IFace struc
    | 91 -> Enum2 struc
    | 92 -> Enum struc
    | 93 -> Ref (Enum struc)
    | 95 -> HLLFunc
    | 98 -> Unknown98
    | 100 -> IFaceWrap struc
    | n -> failwith (sprintf "Invalid or unknown data type in ain file: %d" n)

  let rec to_string = function
    | Void -> "void"
    | Int -> "int"
    | Float -> "float"
    | String -> "string"
    | Struct no ->
        sprintf "struct<%d>" no (* FIXME: look up name in ain object *)
    | IMainSystem -> "IMainSystem"
    | FuncType no -> sprintf "functype<%d>" no (* FIXME *)
    | Bool -> "bool"
    | LongInt -> "lint"
    | Delegate no -> sprintf "delegate<%d>" no (* FIXME *)
    | HLLFunc2 -> "hll_func2"
    | HLLParam -> "hll_param"
    | Array t -> sprintf "array<%s>" (to_string t)
    | Ref t -> sprintf "ref<%s>" (to_string t)
    | Wrap t -> sprintf "wrap<%s>" (to_string t)
    | Option t -> sprintf "option<%s>" (to_string t)
    | Unknown87 t -> sprintf "unknown87<%s>" (to_string t)
    | IFace no ->
        sprintf "interface<%d>" no (* FIXME: look up name in ain object *)
    | Enum2 no -> sprintf "enum2<%d>" no (* FIXME *)
    | Enum no -> sprintf "enum<%d>" no (* FIXME *)
    | HLLFunc -> "hll_func"
    | Unknown98 -> "unknown_98"
    | IFaceWrap no ->
        sprintf "interface_wrap<%d>" no (* FIXME: look up name in ain object *)
    | Function -> "function"
    | Method -> "method"
    | NullType -> "null"
end

module Variable = struct
  type initval = Void | Int of int32 | Float of float | String of string

  type t = {
    index : int;
    name : string;
    name2 : string option;
    value_type : Type.t;
    initval : initval option;
  }

  let make ?(index = -1) name value_type =
    { index; name; name2 = Some ""; value_type; initval = None }
end

module Global = struct
  type t = { variable : Variable.t; group_index : int }

  let create variable group_index = { variable; group_index }
end

module Function = struct
  type t = {
    index : int;
    name : string;
    address : int;
    nr_args : int;
    vars : Variable.t list;
    return_type : Type.t;
    is_label : bool;
    is_lambda : bool;
    crc : int32;
    struct_type : int option;
    enum_type : int option;
  }

  let create ?(index = -1) name =
    {
      index;
      name;
      address = -1;
      nr_args = 0;
      vars = [];
      return_type = Void;
      is_label = false;
      is_lambda = false;
      crc = 0l;
      struct_type = None;
      enum_type = None;
    }

  let set_undefined f =
    { f with address = -1; vars = List.take f.vars f.nr_args }

  let is_defined f = f.address > 1

  let logical_parameters f =
    List.filter (List.take f.vars f.nr_args) ~f:(fun (v : Variable.t) ->
        match v.value_type with Void -> false | _ -> true)
end

module Struct = struct
  type interface = { struct_type : int; vtable_offset : int }

  type t = {
    index : int;
    name : string;
    interfaces : interface list;
    constructor : int;
    destructor : int;
    members : Variable.t list;
    vmethods : int list;
  }

  let create ?(index = -1) name =
    {
      index;
      name;
      interfaces = [];
      constructor = -1;
      destructor = -1;
      members = [];
      vmethods = [];
    }
end

module Library = struct
  module Argument = struct
    type t = { name : string; value_type : Type.t }

    let create name value_type = { name; value_type }
  end

  module Function = struct
    type t = {
      index : int;
      lib_no : int;
      name : string;
      return_type : Type.t;
      arguments : Argument.t list;
    }

    let create name return_type arguments =
      { index = -1; lib_no = -1; name; return_type; arguments }
  end

  type t = { index : int; name : string; functions : Function.t array }
end

module Switch = struct
  type case_type = IntCase | StringCase

  type t = {
    index : int;
    case_type : case_type;
    mutable default_address : int;
    mutable cases : (int32 * int) list;
  }

  let case_type_of_int = function
    | 2 -> IntCase
    | 4 -> StringCase
    | _ -> failwith "invalid switch case type"

  let int_of_case_type = function IntCase -> 2 | StringCase -> 4
end

module FunctionType = struct
  type t = {
    index : int;
    name : string;
    return_type : Type.t;
    nr_arguments : int;
    variables : Variable.t list;
  }

  let create name =
    { index = -1; name; return_type = Void; nr_arguments = 0; variables = [] }

  let logical_parameters f =
    let not_void (v : Variable.t) =
      match v.value_type with Void -> false | _ -> true
    in
    List.filter f.variables ~f:not_void
end

module Enum = struct
  type t = { _index : int; name : string; _symbols : string list }
end

type t = {
  is_ain2 : bool;
  mutable major_version : int;
  mutable minor_version : int;
  mutable keyc : int32;
  code : Buffer.t;
  mutable functions : Function.t Dynarray.t;
  mutable globals : Global.t Dynarray.t;
  mutable structures : Struct.t Dynarray.t;
  mutable messages : string Dynarray.t;
  mutable msg1_uk : int32;
  mutable main : int;
  mutable msgf : int;
  mutable libraries : Library.t Dynarray.t;
  mutable switches : Switch.t Dynarray.t;
  mutable game_version : int;
  mutable scenario_labels : (string * int) Dynarray.t;
  mutable strings : string Dynarray.t;
  mutable filenames : string Dynarray.t;
  mutable ojmp : int;
  mutable function_types : FunctionType.t Dynarray.t;
  mutable delegates : FunctionType.t Dynarray.t;
  mutable global_group_names : string Dynarray.t;
  mutable enums : Enum.t array;
  string_table : (string, int) Hashtbl.t;
  function_by_name : (string, int) Hashtbl.t;
  global_by_name : (string, int) Hashtbl.t;
  struct_by_name : (string, int) Hashtbl.t;
  library_by_name : (string, int) Hashtbl.t;
  functype_by_name : (string, int) Hashtbl.t;
  delegate_by_name : (string, int) Hashtbl.t;
}

let try_add_name_index tbl name index =
  Hashtbl.add tbl ~key:name ~data:index |> ignore

let create ?is_ain2 ?(keyc = 0l) ?(game_version = 0) major_version minor_version
    =
  let functions = Dynarray.of_list [ Function.create ~index:0 "NULL" ] in
  let function_by_name = Hashtbl.create (module String) in
  try_add_name_index function_by_name "NULL" 0;
  {
    is_ain2 = Option.value is_ain2 ~default:(major_version >= 5);
    major_version;
    minor_version;
    keyc;
    code = Buffer.create 4096;
    functions;
    globals = Dynarray.create ();
    structures = Dynarray.create ();
    messages = Dynarray.make 1 "";
    msg1_uk = 0l;
    main = 0;
    msgf = 0;
    libraries = Dynarray.create ();
    switches = Dynarray.create ();
    game_version;
    scenario_labels = Dynarray.create ();
    strings = Dynarray.make 1 "";
    filenames = Dynarray.create ();
    ojmp = -1;
    function_types = Dynarray.create ();
    delegates = Dynarray.create ();
    global_group_names = Dynarray.create ();
    enums = [||];
    string_table = Hashtbl.create (module String);
    function_by_name;
    global_by_name = Hashtbl.create (module String);
    struct_by_name = Hashtbl.create (module String);
    library_by_name = Hashtbl.create (module String);
    functype_by_name = Hashtbl.create (module String);
    delegate_by_name = Hashtbl.create (module String);
  }

let version ain = ain.major_version
let minor_version ain = ain.minor_version

let version_equal ain (major, minor) =
  ain.major_version = major && ain.minor_version = minor

let version_gte ain (major, minor) =
  if ain.major_version < major then false
  else if phys_equal ain.major_version major && ain.minor_version < minor then
    false
  else true

let version_lt ain v = not (version_gte ain v)

let if_version_gte ain version f default buf =
  if version_gte ain version then f buf else default

let if_version_between ain major_low major_high f default buf =
  if ain.major_version <= major_low || ain.major_version >= major_high then
    default
  else f buf

type buf = { ain : t; data : Stdlib.Bytes.t; mutable pos : int }

let read_int32 buf =
  let i = Stdlib.Bytes.get_int32_le buf.data buf.pos in
  buf.pos <- buf.pos + 4;
  i

let read_int buf = Int32.to_int_exn (read_int32 buf)
let read_bool buf = not (Int32.equal (read_int32 buf) Int32.zero)
let read_float buf = Int32.float_of_bits (read_int32 buf)

let read_cstring buf =
  match Stdlib.Bytes.index_from_opt buf.data buf.pos '\x00' with
  | Some i ->
      let len = i - buf.pos in
      let cstr =
        Stdlib.Bytes.to_string (Stdlib.Bytes.sub buf.data buf.pos len)
      in
      buf.pos <- buf.pos + (len + 1);
      Sjis.to_utf8 cstr
  | None -> failwith "unterminated string"

let read_some_cstring buf = Some (read_cstring buf)

let read_variable_type buf =
  let rec read_variable_type' buf =
    let data = read_int buf in
    let struc = read_int buf in
    let rank = read_int buf in
    let read_array_type buf =
      if rank = 0 then None else Some (read_variable_type' buf)
    in
    let subtype = if_version_gte buf.ain (11, 0) read_array_type None buf in
    let (t : Type.parsed) = { data; struc; rank; subtype } in
    t
  in
  Type.of_parsed (read_variable_type' buf)

let read_return_type buf =
  if version_gte buf.ain (11, 0) then read_variable_type buf
  else
    let data = read_int buf in
    let struc = read_int buf in
    let (t : Type.parsed) = { data; struc; rank = 0; subtype = None } in
    Type.of_parsed t

let read_variables buf count =
  let read_initval (t : Type.t) buf =
    if read_bool buf then
      match t with
      | Ref _ | Struct _ | Delegate _ | Array _ -> Some Variable.Void
      | String -> Some (Variable.String (read_cstring buf))
      | Float -> Some (Variable.Float (read_float buf))
      | _ -> Some (Variable.Int (read_int32 buf))
    else None
  in
  let rec read_variables' count index result =
    if count > 0 then
      let name = read_cstring buf in
      let name2 = if_version_gte buf.ain (12, 0) read_some_cstring None buf in
      let value_type = read_variable_type buf in
      let initval =
        if_version_gte buf.ain (8, 0) (read_initval value_type) None buf
      in
      let (v : Variable.t) = { index; name; name2; value_type; initval } in
      read_variables' (count - 1) (index + 1) (v :: result)
    else List.rev result
  in
  read_variables' count 0 []

let read_functions buf count =
  let rec read_functions' count result index =
    if count > 0 then (
      let address = read_int buf in
      let name = read_cstring buf in
      (* detect game (to apply needed quirks) *)
      (if version_equal buf.ain (14, 1) then
         match name with
         | "C_MedicaMenu@0" (* Evenicle 2 *)
         | "CInvasionHexScene@0" (* Haha Ranman *) | "_ALICETOOLS_AINV14_00" ->
             buf.ain.minor_version <- 0
         | _ -> ());
      let is_label = if_version_between buf.ain 1 7 read_bool false buf in
      let return_type = read_return_type buf in
      let nr_args = read_int buf in
      let nr_vars = read_int buf in
      let is_lambda = if_version_gte buf.ain (11, 0) read_bool false buf in
      let crc = if_version_gte buf.ain (2, 0) read_int32 Int32.zero buf in
      let vars = read_variables buf nr_vars in
      let (f : Function.t) =
        {
          index;
          name;
          address;
          nr_args;
          vars;
          return_type;
          is_label;
          is_lambda;
          crc;
          struct_type = None;
          enum_type = None;
        }
      in
      read_functions' (count - 1) (f :: result) (index + 1))
    else List.rev result
  in
  read_functions' count [] 0

let read_globals buf count =
  let rec read_globals' count index result =
    if count > 0 then
      let name = read_cstring buf in
      let name2 = if_version_gte buf.ain (12, 0) read_some_cstring None buf in
      let value_type = read_variable_type buf in
      let group_index = if_version_gte buf.ain (5, 0) read_int 0 buf in
      let (variable : Variable.t) =
        { index; name; name2; value_type; initval = None }
      in
      let (global : Global.t) = { variable; group_index } in
      read_globals' (count - 1) (index + 1) (global :: result)
    else List.rev result
  in
  read_globals' count 0 []

let read_global_initvals buf count =
  let read_initval = function
    | 11 -> Variable.Float (read_float buf)
    | 12 -> Variable.String (read_cstring buf)
    | _ -> Variable.Int (read_int32 buf)
  in
  let rec read_global_initvals' count =
    if count > 0 then (
      let index = read_int buf in
      let initval = Some (read_initval (read_int buf)) in
      let g = Dynarray.get buf.ain.globals index in
      Dynarray.set buf.ain.globals index
        { g with variable = { g.variable with initval } };
      read_global_initvals' (count - 1))
    else ()
  in
  read_global_initvals' count

let read_structures buf count =
  let rec read_interfaces count result =
    if count > 0 then
      let struct_type = read_int buf in
      let vtable_offset = read_int buf in
      let (i : Struct.interface) = { struct_type; vtable_offset } in
      read_interfaces (count - 1) (i :: result)
    else List.rev result
  in
  let rec read_vmethods count result =
    if count > 0 then
      let no = read_int buf in
      read_vmethods (count - 1) (no :: result)
    else List.rev result
  in
  let rec read_structures' count result index =
    if count > 0 then
      let name = read_cstring buf in
      let nr_interfaces = if_version_gte buf.ain (11, 0) read_int 0 buf in
      let interfaces = read_interfaces nr_interfaces [] in
      let constructor = read_int buf in
      let destructor = read_int buf in
      let nr_members = read_int buf in
      let members = read_variables buf nr_members in
      let nr_vmethods = if_version_gte buf.ain (14, 1) read_int 0 buf in
      let vmethods = read_vmethods nr_vmethods [] in
      let (s : Struct.t) =
        { index; name; interfaces; constructor; destructor; members; vmethods }
      in
      read_structures' (count - 1) (s :: result) (index + 1)
    else List.rev result
  in
  read_structures' count [] 0

let read_cstrings buf count =
  let rec read_cstrings' count result =
    if count > 0 then read_cstrings' (count - 1) (read_cstring buf :: result)
    else List.rev result
  in
  read_cstrings' count []

let read_scenario_labels buf count =
  let rec read_scenario_labels' count result =
    if count > 0 then
      let name = read_cstring buf in
      let address = read_int buf in
      read_scenario_labels' (count - 1) ((name, address) :: result)
    else List.rev result
  in
  read_scenario_labels' count []

let read_msg1_strings buf count =
  let read_msg1_string () =
    let len = read_int buf in
    let data = Bytes.sub buf.data ~pos:buf.pos ~len in
    buf.pos <- buf.pos + len;
    for i = 0 to Bytes.length data - 1 do
      let c = Char.to_int (Bytes.get data i) in
      Bytes.set data i (Char.of_int_exn ((c - 0x60 - i) % 256))
    done;
    Bytes.to_string data |> Sjis.to_utf8
  in
  let rec read_msg1_strings' count result =
    if count > 0 then
      read_msg1_strings' (count - 1) (read_msg1_string () :: result)
    else List.rev result
  in
  read_msg1_strings' count []

let read_libraries buf count =
  let read_library_type buf =
    if version_gte buf.ain (14, 0) then read_variable_type buf
    else
      let data = read_int buf in
      (* XXX: rank is 1 in case it's an array type *)
      let (t : Type.parsed) = { data; struc = -1; rank = 1; subtype = None } in
      Type.of_parsed t
  in
  let rec read_library_arguments count result =
    if count > 0 then
      let name = read_cstring buf in
      let value_type = read_library_type buf in
      let (arg : Library.Argument.t) = { name; value_type } in
      read_library_arguments (count - 1) (arg :: result)
    else List.rev result
  in
  let rec read_libraries' count lib_no result =
    let read_library_functions count =
      Array.init count ~f:(fun fno ->
          let name = read_cstring buf in
          let return_type = read_library_type buf in
          let nr_arguments = read_int buf in
          let arguments = read_library_arguments nr_arguments [] in
          let (f : Library.Function.t) =
            { index = fno; lib_no; name; return_type; arguments }
          in
          f)
    in
    if count > 0 then
      let name = read_cstring buf in
      let nr_functions = read_int buf in
      let functions = read_library_functions nr_functions in
      let (lib : Library.t) = { index = lib_no; name; functions } in
      read_libraries' (count - 1) (lib_no + 1) (lib :: result)
    else List.rev result
  in
  read_libraries' count 0 []

let read_switches buf count =
  let rec read_switches' count index result =
    let rec read_switch_cases count result =
      if count > 0 then
        let value = read_int32 buf in
        let address = read_int buf in
        read_switch_cases (count - 1) ((value, address) :: result)
      else List.rev result
    in
    if count > 0 then
      let case_type = Switch.case_type_of_int (read_int buf) in
      let default_address = read_int buf in
      let nr_cases = read_int buf in
      let cases = read_switch_cases nr_cases [] in
      let (switch : Switch.t) = { index; case_type; default_address; cases } in
      read_switches' (count - 1) (index + 1) (switch :: result)
    else List.rev result
  in
  read_switches' count 0 []

let read_function_types buf count =
  let rec read_function_types' count index result =
    if count > 0 then
      let name = read_cstring buf in
      let return_type = read_return_type buf in
      let nr_arguments = read_int buf in
      let nr_variables = read_int buf in
      let variables = read_variables buf nr_variables in
      let (ft : FunctionType.t) =
        { index; name; return_type; nr_arguments; variables }
      in
      read_function_types' (count - 1) (index + 1) (ft :: result)
    else List.rev result
  in
  read_function_types' count 0 []

let read_enums buf count =
  read_cstrings buf count
  |> List.mapi ~f:(fun _index name ->
      (* TODO: symbols *)
      let e : Enum.t = { _index; name; _symbols = [] } in
      e)

let decrypt = Mt19937.(decrypt ain_decrypt_seed)

(* symmetric *)
let encrypt = decrypt

let load filename =
  let read_section_magic buf =
    let s = Stdlib.Bytes.sub_string buf.data buf.pos 4 in
    buf.pos <- buf.pos + 4;
    s
  in
  let read_section buf =
    match read_section_magic buf with
    | "VERS" ->
        buf.ain.major_version <- read_int buf;
        if buf.ain.major_version = 14 then buf.ain.minor_version <- 1
    | "KEYC" -> buf.ain.keyc <- read_int32 buf
    | "CODE" ->
        let len = read_int buf in
        Buffer.clear buf.ain.code;
        Buffer.add_subbytes buf.ain.code buf.data ~pos:buf.pos ~len;
        buf.pos <- buf.pos + len
    | "FUNC" ->
        let count = read_int buf in
        buf.ain.functions <- Dynarray.of_list (read_functions buf count)
    | "GLOB" ->
        let count = read_int buf in
        buf.ain.globals <- Dynarray.of_list (read_globals buf count)
    | "GSET" ->
        let count = read_int buf in
        read_global_initvals buf count
    | "STRT" ->
        let count = read_int buf in
        buf.ain.structures <- Dynarray.of_list (read_structures buf count)
    | "MSG0" ->
        let count = read_int buf in
        buf.ain.messages <- Dynarray.of_list (read_cstrings buf count)
    | "MSG1" ->
        let count = read_int buf in
        buf.ain.msg1_uk <- read_int32 buf;
        buf.ain.messages <- Dynarray.of_list (read_msg1_strings buf count);
        if buf.ain.major_version = 6 && buf.ain.minor_version < 20 then
          buf.ain.minor_version <- 20
    | "MAIN" -> buf.ain.main <- read_int buf
    | "MSGF" -> buf.ain.msgf <- read_int buf
    | "HLL0" ->
        let count = read_int buf in
        buf.ain.libraries <- Dynarray.of_list (read_libraries buf count)
    | "SWI0" ->
        let count = read_int buf in
        buf.ain.switches <- Dynarray.of_list (read_switches buf count)
    | "SLBL" ->
        let count = read_int buf in
        buf.ain.scenario_labels <-
          Dynarray.of_list (read_scenario_labels buf count)
    | "STR0" ->
        let count = read_int buf in
        buf.ain.strings <- Dynarray.of_list (read_cstrings buf count)
    | "FNAM" ->
        let count = read_int buf in
        buf.ain.filenames <- Dynarray.of_list (read_cstrings buf count)
    | "OJMP" -> buf.ain.ojmp <- read_int buf
    | "GVER" -> buf.ain.game_version <- read_int buf
    | "FNCT" ->
        let (_ : int32) = read_int32 buf in
        (* section size *)
        let count = read_int buf in
        buf.ain.function_types <-
          Dynarray.of_list (read_function_types buf count)
    | "DELG" ->
        let (_ : int32) = read_int32 buf in
        (* section size *)
        let count = read_int buf in
        buf.ain.delegates <- Dynarray.of_list (read_function_types buf count);
        if buf.ain.major_version = 6 && buf.ain.minor_version < 10 then
          buf.ain.minor_version <- 10
    | "OBJG" ->
        let count = read_int buf in
        buf.ain.global_group_names <- Dynarray.of_list (read_cstrings buf count)
    | "ENUM" ->
        let count = read_int buf in
        buf.ain.enums <- Array.of_list (read_enums buf count)
    | s -> failwith (Printf.sprintf "unhandled section: %s" s)
  in
  let rec read_sections buf =
    read_section buf;
    if buf.pos >= Stdlib.Bytes.length buf.data then buf.ain
    else read_sections buf
  in
  let populate_by_name ain =
    Hashtbl.clear ain.function_by_name;
    Hashtbl.clear ain.global_by_name;
    Hashtbl.clear ain.struct_by_name;
    Hashtbl.clear ain.library_by_name;
    Hashtbl.clear ain.functype_by_name;
    Hashtbl.clear ain.delegate_by_name;
    Dynarray.iteri
      (fun i (f : Function.t) ->
        try_add_name_index ain.function_by_name f.name i)
      ain.functions;
    Dynarray.iteri
      (fun i (g : Global.t) ->
        try_add_name_index ain.global_by_name g.variable.name i)
      ain.globals;
    Dynarray.iteri
      (fun i (s : Struct.t) -> try_add_name_index ain.struct_by_name s.name i)
      ain.structures;
    Dynarray.iteri
      (fun i (l : Library.t) -> try_add_name_index ain.library_by_name l.name i)
      ain.libraries;
    Dynarray.iteri
      (fun i (ft : FunctionType.t) ->
        try_add_name_index ain.functype_by_name ft.name i)
      ain.function_types;
    Dynarray.iteri
      (fun i (dg : FunctionType.t) ->
        try_add_name_index ain.delegate_by_name dg.name i)
      ain.delegates
  in
  let load' file =
    let ain = create 4 0 in
    let magic = Buffer.create 4 in
    Option.value_exn (In_channel.input_buffer file magic ~len:4);
    match Buffer.contents magic with
    | "AI2\x00" ->
        (* compressed ain *)
        let read_int ch =
          let buf = Buffer.create 4 in
          Option.value_exn (In_channel.input_buffer ch buf ~len:4);
          Int32.to_int_exn (Stdlib.String.get_int32_le (Buffer.contents buf) 0)
        in
        In_channel.seek file 8L;
        let out_len = read_int file in
        In_channel.seek file 16L;
        let ain_buf = { ain; data = Bytes.create out_len; pos = 0 } in
        let refill buf =
          In_channel.input file ~buf ~pos:0 ~len:(Bytes.length buf)
        in
        let flush buf len =
          Bytes.blit ~src:buf ~src_pos:0 ~dst:ain_buf.data ~dst_pos:ain_buf.pos
            ~len;
          ain_buf.pos <- ain_buf.pos + len
        in
        Zlib.uncompress refill flush;
        ain_buf.pos <- 0;
        read_sections ain_buf
    | "\x7e\xf5\x02\xba" ->
        (* encrypted "VERS" *)
        In_channel.seek file 0L;
        let data = Bytes.of_string (In_channel.input_all file) in
        let buf = { ain; data; pos = 0 } in
        decrypt buf.data;
        read_sections buf
    | "VERS" ->
        (* raw (as produced by `alice ain dump -d`) *)
        In_channel.seek file 0L;
        let data = Bytes.of_string (In_channel.input_all file) in
        let buf = { ain; data; pos = 0 } in
        read_sections buf
    | _ -> failwith "unrecognized .ain format"
  in
  let ain = In_channel.with_file filename ~f:load' in
  populate_by_name ain;
  ain

module BinBuffer = struct
  include Buffer

  let conv_buf32 = Bytes.create 4

  let add_int32 buf i =
    Stdlib.Bytes.set_int32_le conv_buf32 0 i;
    Buffer.add_bytes buf conv_buf32

  let add_int buf i = add_int32 buf (Int32.of_int_trunc i)
  let add_float buf f = add_int32 buf (Int32.bits_of_float f)

  let add_bool buf b =
    Buffer.add_string buf (if b then "\x01\x00\x00\x00" else "\x00\x00\x00\x00")

  let add_cstring buf s =
    Buffer.add_string buf (Sjis.from_utf8 s);
    Buffer.add_char buf '\x00'
end

let write_msg1_string buf s =
  let tmp = Bytes.of_string (Sjis.from_utf8 s) in
  let len = Bytes.length tmp in
  BinBuffer.add_int buf len;
  for i = 0 to len - 1 do
    let c = Char.to_int (Bytes.get tmp i) in
    Bytes.set tmp i (Char.of_int_exn ((c + 0x60 + i) % 256))
  done;
  BinBuffer.add_bytes buf tmp

let rec write_variable_type ?(var = true) buf ain (t : Type.t) =
  (* v11 fallback: [hll_param] has no dedicated type code as a variable
     slot. Foreach vars whose element type couldn't be narrowed end up
     here - encode them as [int] so the file is at least loadable. *)
  let t =
    if version_gte ain (11, 0) then
      match t with
      | Type.HLLParam -> Type.Int
      | Type.Ref Type.HLLParam -> Type.Ref Type.Int
      | _ -> t
    else t
  in
  BinBuffer.add_int buf (Type.int_of_data_type ain.major_version t);
  BinBuffer.add_int buf (Type.int_of_struct_type ain.major_version t ~var);
  if version_gte ain (11, 0) then
    (* v11 format: no rank field; instead a presence-bool for the
       recursive subtype. Ref-wrapped composites unwrap to their inner
       for subtype encoding. *)
    match t with
    | Array t | Wrap t | Option t | Unknown87 t
    | Ref (Array t | Wrap t | Option t | Unknown87 t) ->
        BinBuffer.add_bool buf true;
        write_variable_type ~var buf ain t
    | _ -> BinBuffer.add_bool buf false
  else BinBuffer.add_int buf (Type.int_of_rank ain.major_version t)

let write_return_type buf ain (t : Type.t) =
  if version_gte ain (11, 0) then write_variable_type ~var:false buf ain t
  else (
    BinBuffer.add_int buf (Type.int_of_data_type ain.major_version t);
    BinBuffer.add_int buf (Type.int_of_struct_type ain.major_version t))

let write_string_option buf opt = Option.iter opt ~f:(BinBuffer.add_cstring buf)

let write_variable buf ain (v : Variable.t) =
  let module BB = BinBuffer in
  (* v11: [HLLParam] has no dedicated variable-slot type code, so
     [write_variable_type] coerces it to [Int]. The matching initval
     byte must also be [Int 0] - [Void] would leave the initval-bool
     set but skip the 4-byte int payload, corrupting every subsequent
     variable's encoding. *)
  let initval =
    if version_gte ain (11, 0) then
      match (v.value_type, v.initval) with
      | Type.HLLParam, Some Variable.Void
      | Type.Ref Type.HLLParam, Some Variable.Void ->
          Some (Variable.Int 0l)
      | _ -> v.initval
    else v.initval
  in
  let write_variable_initval (opt : Variable.initval option) =
    BB.add_bool buf (Option.is_some opt);
    match opt with
    | Some Void -> ()
    | Some (Int i) -> BB.add_int32 buf i
    | Some (Float f) -> BB.add_float buf f
    | Some (String s) -> BB.add_cstring buf s
    | None -> ()
  in
  BB.add_cstring buf v.name;
  if_version_gte ain (12, 0) (write_string_option buf) () v.name2;
  write_variable_type buf ain v.value_type;
  if_version_gte ain (8, 0) write_variable_initval () initval

let write_function buf ain (f : Function.t) =
  let module BB = BinBuffer in
  BB.add_int buf f.address;
  BB.add_cstring buf f.name;
  if_version_between ain 1 7 (BB.add_bool buf) () f.is_label;
  write_return_type buf ain f.return_type;
  BB.add_int buf f.nr_args;
  BB.add_int buf (List.length f.vars);
  if_version_gte ain (11, 0) (BB.add_bool buf) () f.is_lambda;
  if_version_gte ain (2, 0) (BB.add_int32 buf) () f.crc;
  List.iter f.vars ~f:(write_variable buf ain)

let write_global buf ain (g : Global.t) =
  BinBuffer.add_cstring buf g.variable.name;
  if_version_gte ain (12, 0) (write_string_option buf) () g.variable.name2;
  write_variable_type buf ain g.variable.value_type;
  if_version_gte ain (5, 0) (BinBuffer.add_int buf) () g.group_index

let write_initval buf (i, data_type, (initval : Variable.initval)) =
  let module BB = BinBuffer in
  BB.add_int buf i;
  BB.add_int buf data_type;
  match initval with
  | Void -> ()
  | Int i -> BB.add_int32 buf i
  | Float f -> BB.add_float buf f
  | String s -> BB.add_cstring buf s

let write_structure buf ain (s : Struct.t) =
  let module BB = BinBuffer in
  let write_interfaces interfaces =
    let write_interface (iface : Struct.interface) =
      BB.add_int buf iface.struct_type;
      BB.add_int buf iface.vtable_offset
    in
    BB.add_int buf (List.length interfaces);
    List.iter interfaces ~f:write_interface
  in
  let write_vmethods vmethods =
    BB.add_int buf (List.length vmethods);
    List.iter vmethods ~f:(BB.add_int buf)
  in
  BB.add_cstring buf s.name;
  if_version_gte ain (11, 0) write_interfaces () s.interfaces;
  BB.add_int buf s.constructor;
  BB.add_int buf s.destructor;
  BB.add_int buf (List.length s.members);
  List.iter s.members ~f:(write_variable buf ain);
  if_version_gte ain (14, 1) write_vmethods () s.vmethods

let write_library buf ain (lib : Library.t) =
  let module BB = BinBuffer in
  let write_library_type (t : Type.t) =
    if version_gte ain (14, 0) then write_variable_type buf ain t
    else BB.add_int buf (Type.int_of_data_type ain.major_version t)
  in
  let write_library_argument (arg : Library.Argument.t) =
    BB.add_cstring buf arg.name;
    write_library_type arg.value_type
  in
  let write_library_function (f : Library.Function.t) =
    BB.add_cstring buf f.name;
    if version_gte ain (14, 0) then write_variable_type buf ain f.return_type
    else BB.add_int buf (Type.int_of_data_type ain.major_version f.return_type);
    BB.add_int buf (List.length f.arguments);
    List.iter f.arguments ~f:write_library_argument
  in
  BB.add_cstring buf lib.name;
  BB.add_int buf (Array.length lib.functions);
  Array.iter lib.functions ~f:write_library_function

let write_scenario_label buf (name, address) =
  let module BB = BinBuffer in
  BB.add_cstring buf name;
  BB.add_int buf address

let write_scenario_labels buf labels =
  BinBuffer.add_int buf (Dynarray.length labels);
  (* The section is sorted by name in Shift_JIS byte order. *)
  Dynarray.to_list labels
  |> List.sort ~compare:(fun (a, _) (b, _) ->
      String.compare (Sjis.from_utf8 a) (Sjis.from_utf8 b))
  |> List.iter ~f:(write_scenario_label buf)

let write_switch buf (sw : Switch.t) =
  let module BB = BinBuffer in
  let write_switch_case (value, addr) =
    BB.add_int32 buf value;
    BB.add_int buf addr
  in
  BB.add_int buf (Switch.int_of_case_type sw.case_type);
  BB.add_int buf sw.default_address;
  BB.add_int buf (List.length sw.cases);
  List.iter sw.cases ~f:write_switch_case

let write_functype buf ain (ft : FunctionType.t) =
  let module BB = BinBuffer in
  BB.add_cstring buf ft.name;
  write_return_type buf ain ft.return_type;
  BB.add_int buf ft.nr_arguments;
  BB.add_int buf (List.length ft.variables);
  List.iter ft.variables ~f:(write_variable buf ain)

(* Write an FNCT/DELG section. Its second field is the byte size of the section
   excluding the 4-byte magic tag. *)
let write_functype_section buf ain tag (entries : FunctionType.t Dynarray.t) =
  let module BB = BinBuffer in
  BB.add_string buf tag;
  let body = BB.create 1024 in
  BB.add_int body (Dynarray.length entries);
  Dynarray.iter (write_functype body ain) entries;
  BB.add_int buf (BB.length body + 4);
  Buffer.add_buffer buf body

let to_buffer ain =
  let global_initvals =
    let acc = ref [] in
    Dynarray.iteri
      (fun i (g : Global.t) ->
        match g.variable.initval with
        | Some initval ->
            let data_type =
              Type.int_of_data_type ain.major_version g.variable.value_type
            in
            acc := (i, data_type, initval) :: !acc
        | None -> ())
      ain.globals;
    List.rev !acc
  in
  let buf = BinBuffer.create 1024 in
  let module BB = BinBuffer in
  BB.add_string buf "VERS";
  BB.add_int buf ain.major_version;
  if ain.major_version > 1 && ain.major_version < 12 then (
    BB.add_string buf "KEYC";
    BB.add_int32 buf ain.keyc);
  BB.add_string buf "CODE";
  BB.add_int buf (Buffer.length ain.code);
  Buffer.add_buffer buf ain.code;
  BB.add_string buf "FUNC";
  BB.add_int buf (Dynarray.length ain.functions);
  Dynarray.iter (write_function buf ain) ain.functions;
  BB.add_string buf "GLOB";
  BB.add_int buf (Dynarray.length ain.globals);
  Dynarray.iter (write_global buf ain) ain.globals;
  if ain.major_version < 12 then (
    BB.add_string buf "GSET";
    BB.add_int buf (List.length global_initvals);
    List.iter global_initvals ~f:(write_initval buf));
  BB.add_string buf "STRT";
  BB.add_int buf (Dynarray.length ain.structures);
  Dynarray.iter (write_structure buf ain) ain.structures;
  if version_lt ain (6, 20) then (
    BB.add_string buf "MSG0";
    BB.add_int buf (Dynarray.length ain.messages);
    Dynarray.iter (BB.add_cstring buf) ain.messages)
  else (
    BB.add_string buf "MSG1";
    BB.add_int buf (Dynarray.length ain.messages);
    BB.add_int32 buf ain.msg1_uk;
    Dynarray.iter (write_msg1_string buf) ain.messages);
  BB.add_string buf "MAIN";
  BB.add_int buf ain.main;
  if ain.major_version < 12 then (
    BB.add_string buf "MSGF";
    BB.add_int buf ain.msgf);
  BB.add_string buf "HLL0";
  BB.add_int buf (Dynarray.length ain.libraries);
  Dynarray.iter (write_library buf ain) ain.libraries;
  BB.add_string buf "SWI0";
  BB.add_int buf (Dynarray.length ain.switches);
  Dynarray.iter (write_switch buf) ain.switches;
  BB.add_string buf "GVER";
  BB.add_int buf ain.game_version;
  if ain.major_version = 1 then (
    BB.add_string buf "SLBL";
    write_scenario_labels buf ain.scenario_labels);
  if ain.major_version >= 1 then (
    BB.add_string buf "STR0";
    BB.add_int buf (Dynarray.length ain.strings);
    Dynarray.iter (BB.add_cstring buf) ain.strings);
  if ain.major_version >= 1 && ain.major_version < 12 then (
    BB.add_string buf "FNAM";
    BB.add_int buf (Dynarray.length ain.filenames);
    Dynarray.iter (BB.add_cstring buf) ain.filenames);
  if ain.major_version >= 1 && ain.major_version < 7 then (
    BB.add_string buf "OJMP";
    BB.add_int buf ain.ojmp);
  (* XXX: section disappears in Rance IX (mid v6) *)
  if Dynarray.length ain.function_types > 0 then
    write_functype_section buf ain "FNCT" ain.function_types;
  (* XXX: section first appears in Oyako Rankan (mid v6) *)
  if Dynarray.length ain.delegates > 0 then
    write_functype_section buf ain "DELG" ain.delegates;
  if version_gte ain (5, 0) then (
    BB.add_string buf "OBJG";
    BB.add_int buf (Dynarray.length ain.global_group_names);
    Dynarray.iter (BB.add_cstring buf) ain.global_group_names);
  if version_gte ain (12, 0) then (
    BB.add_string buf "ENUM";
    BB.add_int buf (Array.length ain.enums);
    Array.iter ain.enums ~f:(fun e -> BB.add_cstring buf e.name));
  buf

let write ?(raw = false) ain out =
  let write_buffer buf =
    if raw then Out_channel.output_buffer out buf
    else if ain.is_ain2 then (
      let write_int32 i =
        let tmp = Bytes.create 4 in
        Stdlib.Bytes.set_int32_le tmp 0 i;
        Out_channel.output_bytes out tmp
      in
      (* write header *)
      Out_channel.output_string out "AI2\x00";
      write_int32 0l;
      (* ??? *)
      write_int32 (Int32.of_int_exn (BinBuffer.length buf));
      (* uncompressed size *)
      write_int32 0l;
      (* compressed size determined later *)
      (* compress *)
      let ain_buf = { ain; data = BinBuffer.contents_bytes buf; pos = 0 } in
      let refill zipbuf =
        let in_len = Bytes.length ain_buf.data - ain_buf.pos in
        let out_len = Bytes.length zipbuf in
        let len = min in_len out_len in
        Bytes.blit ~src:ain_buf.data ~src_pos:ain_buf.pos ~dst:zipbuf ~dst_pos:0
          ~len;
        ain_buf.pos <- ain_buf.pos + len;
        len
      in
      let flush zipbuf len = Out_channel.output out ~buf:zipbuf ~pos:0 ~len in
      Zlib.compress refill flush;
      let compressed_size = Int64.(to_int32_exn (Out_channel.pos out - 16L)) in
      Out_channel.seek out 12L;
      write_int32 compressed_size)
    else
      (* encrypt *)
      let data = BinBuffer.contents_bytes buf in
      encrypt data;
      Out_channel.output_bytes out data
  in
  to_buffer ain |> write_buffer

let write_file ain file = Out_channel.with_file file ~f:(write ain) ~binary:true

(* globals *)

let get_global ain name =
  Hashtbl.find ain.global_by_name name
  |> Option.map ~f:(fun i -> (Dynarray.get ain.globals i).variable)

let get_global_by_index ain no = (Dynarray.get ain.globals no).variable

let set_global_type ain name t =
  match Hashtbl.find ain.global_by_name name with
  | Some i ->
      let g = Dynarray.get ain.globals i in
      Dynarray.set ain.globals i
        { g with variable = { g.variable with value_type = t } }
  | None -> failwith (sprintf "No global named '%s' in ain object" name)

let set_global_initval ain name initval =
  match Hashtbl.find ain.global_by_name name with
  | Some i ->
      let g = Dynarray.get ain.globals i in
      Dynarray.set ain.globals i
        { g with variable = { g.variable with initval = Some initval } }
  | None -> failwith (sprintf "No global named '%s' in ain object" name)

let write_new_global ain (v : Variable.t) =
  let index = Dynarray.length ain.globals in
  let g : Global.t = { variable = { v with index }; group_index = 0 } in
  Dynarray.add_last ain.globals g;
  try_add_name_index ain.global_by_name v.name index;
  index

let add_global ain name group_index =
  (* v11 dedup by name: overlapping type-declare passes can register
     the same global twice — return the existing index to avoid
     duplicate entries. Pre-v11 keeps the historical append-only
     behavior; the duplicate check happens at the [declarations.ml]
     callsite via [Hashtbl.add ctx.globals]. *)
  let existing =
    if version_gte ain (11, 0) then Hashtbl.find ain.global_by_name name
    else None
  in
  match existing with
  | Some i -> i
  | None ->
      let index = Dynarray.length ain.globals in
      let variable = Variable.make ~index name Void in
      let g = Global.create variable group_index in
      Dynarray.add_last ain.globals g;
      try_add_name_index ain.global_by_name name index;
      index

let add_global_group ain name =
  let exception Found of int in
  match
    Dynarray.iteri
      (fun i n -> if String.equal n name then Stdlib.raise_notrace (Found i))
      ain.global_group_names
  with
  | () ->
      let index = Dynarray.length ain.global_group_names in
      Dynarray.add_last ain.global_group_names name;
      index
  | exception Found i -> i

(* functions *)

let get_function ain name =
  Hashtbl.find ain.function_by_name name
  |> Option.map ~f:(Dynarray.get ain.functions)

let get_function_by_index ain no = Dynarray.get ain.functions no
let write_function ain (f : Function.t) = Dynarray.set ain.functions f.index f

let write_new_function ain (f : Function.t) =
  let index = Dynarray.length ain.functions in
  let f = { f with index } in
  Dynarray.add_last ain.functions f;
  try_add_name_index ain.function_by_name f.name index;
  index

(* v11 stub-aware [add_function]: if an unclaimed stub (address = -1)
   with matching name (and matching arity when [~nr_args] is passed)
   already exists, reuse its slot and mark it claimed (address = -2)
   instead of appending a new entry. This lets ghost-lambda
   pre-registration and the later real-lambda [add_function] call
   share the same function-table slot, matching the original
   compiler's behavior. Without this, ghost + real allocate two
   entries with the same name, breaking lookups and dispatch. *)
let add_function ?(nr_args = -1) ain name =
  let matching_stub =
    if version_gte ain (11, 0) then (
      let len = Dynarray.length ain.functions in
      let rec find i =
        if i >= len then None
        else
          let f = Dynarray.get ain.functions i in
          if
            String.equal f.name name && f.address = -1
            && (nr_args < 0 || f.nr_args = nr_args)
          then Some f
          else find (i + 1)
      in
      find 0)
    else None
  in
  match matching_stub with
  | Some f ->
      Dynarray.set ain.functions f.index { f with address = -2 };
      Dynarray.get ain.functions f.index
  | None ->
      let no = Function.create name |> write_new_function ain in
      Dynarray.get ain.functions no

(* scenario labels (ain v1 only) *)

let add_scenario_label ain name address =
  Dynarray.add_last ain.scenario_labels (name, address)

(* structures *)

let get_struct ain name =
  Hashtbl.find ain.struct_by_name name
  |> Option.map ~f:(Dynarray.get ain.structures)

let get_struct_index ain name = Hashtbl.find ain.struct_by_name name
let get_struct_by_index ain no = Dynarray.get ain.structures no
let write_struct ain (s : Struct.t) = Dynarray.set ain.structures s.index s

let write_new_struct ain (s : Struct.t) =
  let index = Dynarray.length ain.structures in
  let s = { s with index } in
  Dynarray.add_last ain.structures s;
  try_add_name_index ain.struct_by_name s.name index;
  index

let add_struct ain name =
  (* v11 dedup by name: overlapping type-declare passes can register
     the same struct twice — return the existing entry. Pre-v11 keeps
     the historical append-only behavior. *)
  let existing =
    if version_gte ain (11, 0) then get_struct ain name else None
  in
  match existing with
  | Some s -> s
  | None ->
      let no = Struct.create name |> write_new_struct ain in
      Dynarray.get ain.structures no

(* switches *)

let write_switch ain (switch : Switch.t) =
  Dynarray.set ain.switches switch.index switch

let add_switch ain case_type =
  let index = Dynarray.length ain.switches in
  let s : Switch.t = { index; case_type; default_address = -1; cases = [] } in
  Dynarray.add_last ain.switches s;
  s

(* enums *)

let get_enum ain name =
  let exception Found of int in
  match
    Array.iteri ain.enums ~f:(fun i e ->
        if String.equal e.name name then Stdlib.raise_notrace (Found i))
  with
  | () -> None
  | exception Found i -> Some i

(* libraries *)

let get_library_index ain name = Hashtbl.find ain.library_by_name name

let get_library_function_index ain lib_no name =
  Array.findi (Dynarray.get ain.libraries lib_no).functions ~f:(fun _ f ->
      String.equal f.name name)
  |> Option.map ~f:fst

let get_library_by_index ain no = Dynarray.get ain.libraries no

let write_library ain (lib : Library.t) =
  Dynarray.set ain.libraries lib.index lib

let write_new_library ain (lib : Library.t) =
  let index = Dynarray.length ain.libraries in
  let lib = { lib with index } in
  Dynarray.add_last ain.libraries lib;
  try_add_name_index ain.library_by_name lib.name index;
  index

let add_library ain name =
  match get_library_index ain name with
  | Some i -> Dynarray.get ain.libraries i
  | None ->
      let lib : Library.t = { index = -1; name; functions = [||] } in
      let no = write_new_library ain lib in
      Dynarray.get ain.libraries no

let function_of_hll_function_index ain lib_no fun_no : Function.t =
  let lib_fun = (Dynarray.get ain.libraries lib_no).functions.(fun_no) in
  let var_of_hll_arg index (arg : Library.Argument.t) : Variable.t =
    {
      index;
      name = arg.name;
      name2 = None;
      value_type = arg.value_type;
      initval = None;
    }
  in
  {
    index = -1;
    name = lib_fun.name;
    address = -1;
    nr_args = List.length lib_fun.arguments;
    vars = List.mapi lib_fun.arguments ~f:var_of_hll_arg;
    return_type = lib_fun.return_type;
    is_label = false;
    is_lambda = false;
    crc = 0l;
    struct_type = None;
    enum_type = None;
  }

(* function types *)

let get_functype ain name =
  Hashtbl.find ain.functype_by_name name
  |> Option.map ~f:(Dynarray.get ain.function_types)

let get_functype_index ain name = Hashtbl.find ain.functype_by_name name
let get_functype_by_index ain no = Dynarray.get ain.function_types no

let write_functype ain (ft : FunctionType.t) =
  Dynarray.set ain.function_types ft.index ft

let write_new_functype ain (ft : FunctionType.t) =
  let index = Dynarray.length ain.function_types in
  let ft = { ft with index } in
  Dynarray.add_last ain.function_types ft;
  try_add_name_index ain.functype_by_name ft.name index;
  index

let add_functype ain name =
  match get_functype ain name with
  | Some ft -> ft
  | None ->
      let no = FunctionType.create name |> write_new_functype ain in
      Dynarray.get ain.function_types no

(* TODO: should be FunctionType.to_function *)
let function_of_functype (ft : FunctionType.t) no : Function.t =
  {
    index = no;
    address = -1;
    name = ft.name;
    nr_args = ft.nr_arguments;
    vars = ft.variables;
    return_type = ft.return_type;
    is_label = false;
    is_lambda = false;
    crc = 0l;
    struct_type = None;
    enum_type = None;
  }

let function_of_functype_index ain no =
  function_of_functype (Dynarray.get ain.function_types no) no

(* delegates *)

let get_delegate ain name =
  Hashtbl.find ain.delegate_by_name name
  |> Option.map ~f:(Dynarray.get ain.delegates)

let get_delegate_index ain name = Hashtbl.find ain.delegate_by_name name
let get_delegate_by_index ain no = Dynarray.get ain.delegates no

let write_delegate ain (dg : FunctionType.t) =
  Dynarray.set ain.delegates dg.index dg

let write_new_delegate ain (dg : FunctionType.t) =
  let index = Dynarray.length ain.delegates in
  let dg = { dg with index } in
  Dynarray.add_last ain.delegates dg;
  try_add_name_index ain.delegate_by_name dg.name index;
  index

let add_delegate ain name =
  match get_delegate ain name with
  | Some d -> d
  | None ->
      let no = FunctionType.create name |> write_new_delegate ain in
      Dynarray.get ain.delegates no

let function_of_delegate_index ain no =
  function_of_functype (Dynarray.get ain.delegates no) no

(* strings, messages, files *)

(* FIXME: this shouldn't return an option? *)
let get_string ain no =
  let open Dynarray in
  if no >= length ain.strings then None else Some (get ain.strings no)

let init_string_table ain =
  if Hashtbl.length ain.string_table = 0 then
    Dynarray.iteri
      (fun index str ->
        match Hashtbl.add ain.string_table ~key:str ~data:index with
        | `Duplicate -> ()
        | `Ok -> ())
      ain.strings

let add_string ain str =
  init_string_table ain;
  match Hashtbl.find ain.string_table str with
  | Some index -> index
  | None ->
      let index = Dynarray.length ain.strings in
      Dynarray.add_last ain.strings str;
      Hashtbl.add_exn ain.string_table ~key:str ~data:index;
      index

let get_string_no ain str =
  init_string_table ain;
  Hashtbl.find ain.string_table str

(* FIXME: this shouldn't return an option? *)
let get_message ain no =
  let open Dynarray in
  if no >= length ain.messages then None else Some (get ain.messages no)

let add_message ain str =
  let index = Dynarray.length ain.messages in
  Dynarray.add_last ain.messages str;
  index

let get_file ain no =
  if no >= Dynarray.length ain.filenames then None
  else Some (Dynarray.get ain.filenames no)

let add_file ain name =
  let index = Dynarray.length ain.filenames in
  Dynarray.add_last ain.filenames name;
  index

(* code *)

let get_code ain = Buffer.contents_bytes ain.code

let append_bytecode ain (buf : CBuffer.t) =
  Buffer.add_subbytes ain.code buf.buf ~pos:0 ~len:buf.pos

let code_size ain = Buffer.length ain.code
let set_main_function ain no = ain.main <- no
let set_message_function ain no = ain.msgf <- no
let nr_globals ain = Dynarray.length ain.globals
let nr_functions ain = Dynarray.length ain.functions
let nr_structs ain = Dynarray.length ain.structures
let nr_functypes ain = Dynarray.length ain.function_types
let nr_delegates ain = Dynarray.length ain.delegates
let nr_libraries ain = Dynarray.length ain.libraries

let dynarray_iter ?(from = 0) a ~f =
  let finish = Dynarray.length a - 1 in
  for i = from to finish do
    f (Dynarray.get a i)
  done

let global_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.globals ~f
let function_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.functions ~f
let struct_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.structures ~f
let functype_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.function_types ~f
let delegate_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.delegates ~f
let library_iter ?(from = 0) ~f ain = dynarray_iter ~from ain.libraries ~f

exception File_error
exception Unrecognized_format
exception Invalid_format
