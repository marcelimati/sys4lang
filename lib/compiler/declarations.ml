(* Copyright (C) 2021 Nunuhara Cabbage <nunuhara@haniwa.technology>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, see <http://gnu.org/licenses/>.
 *)

open Common
open Base
open Jaf
open CompileError

let lambda_index = ref 0
let _lambda_file_index : (string, int) Hashtbl.t = Hashtbl.create (module String)

(*
 * AST pass over top-level declarations register names in the .ain file.
 *)
class type_declare_visitor ctx =
  object (self)
    inherit ivisitor ctx as super
    val mutable gg_index = -1

    method! visit_fundecl decl =
      if decl.is_lambda then (
        lambda_index := !lambda_index + 1;
        (* Use annotated name if provided, otherwise generate one *)
        let lambda_name =
          if not (String.equal decl.name "<lambda>") then
            (* Name was set by @lambda annotation in decompiled source *)
            decl.name
          else
            let pos = fst decl.loc in
            if String.equal pos.Lexing.pos_fname "" then
              Printf.sprintf "<lambda : %d>" !lambda_index
            else
              let fname = pos.Lexing.pos_fname in
              let rel =
                String.substr_replace_all fname ~pattern:"/" ~with_:"\\"
              in
              let asra_pat = "Asra3\\" in
              let rel =
                match String.substr_index rel ~pattern:asra_pat with
                | Some i -> String.drop_prefix rel (i + String.length asra_pat)
                | None -> Stdlib.Filename.basename fname
              in
              Printf.sprintf "<lambda : %s%d>" rel !lambda_index
        in
        decl.name <- lambda_name;
        (* For annotated lambdas, the name already includes the class prefix *)
        if String.equal lambda_name "<lambda>" || not (String.mem lambda_name '@')
        then
          decl.class_name <-
            (Option.value_exn self#env#current_function).class_name);
      let name = mangled_name decl in
      let nr_args = List.length decl.params in
      if Option.is_some decl.body then (
        (* v11: create undefined ghost lambda entry before the real one.
           The original compiler creates these for delegate callback matching.
           Ghost has addr=-1 (no code), same params, same return type. *)
        if decl.is_lambda && Ain.version ctx.ain > 8 then (
          let ghost = Ain.Function.create name in
          let ghost = { ghost with
            nr_args;
            return_type = Jaf.jaf_to_ain_type decl.return.ty;
            is_lambda = true;
          } in
          ignore (Ain.write_new_function ctx.ain ghost));
        (* Pre-register the @2 array initializer function immediately before
           a constructor definition. The original compiler interleaves these
           (e.g. CASTimer@2 at index N, CASTimer@0 at N+1). The body of the
           @2 function is generated later by arrayInit.ml, which will find
           and reuse this pre-registered index. *)
        (if is_constructor decl then
           let init_name =
             Option.value_exn decl.class_name ^ "@2"
           in
           match Ain.get_function ctx.ain init_name with
           | Some _ -> ()
           | None -> ignore (Ain.add_function ctx.ain init_name));
        decl.index <- Some (Ain.add_function ~nr_args ctx.ain name).index);
      Hashtbl.update ctx.functions name ~f:(function
        | Some prev_decl ->
            if
              not (ft_compatible (ft_of_fundecl decl) (ft_of_fundecl prev_decl))
            then
              if Ain.version ctx.ain < 11 then
                compile_error "Function signature mismatch"
                  (ASTDeclaration (Function decl))
              else (
                (* Overloaded function (v11+): use arity-based key. *)
                let ft = ft_of_fundecl decl in
                let nr_params = List.length decl.params in
                let rec find_key suffix =
                  let key =
                    if suffix = 0 then Printf.sprintf "%s#%d" name nr_params
                    else Printf.sprintf "%s#%d_%d" name nr_params suffix
                  in
                  match Hashtbl.find ctx.functions key with
                  | Some existing
                    when ft_compatible ft (ft_of_fundecl existing) ->
                      (key, Some existing)
                  | None -> (key, None)
                  | _ -> find_key (suffix + 1)
                in
                let key, matching = find_key 0 in
                (match matching with
                | Some overload_decl when Option.is_some decl.body ->
                    (* Providing body for a declared overload -
                       decl.index was already set at line 67, keep it *)
                    overload_decl.index <- decl.index;
                    decl.params <- overload_decl.params;
                    Hashtbl.set ctx.functions ~key ~data:decl
                | _ ->
                    (* New overload declaration or definition -
                       decl.index was already set at line 67 if body exists *)
                    Hashtbl.set ctx.functions ~key ~data:decl);
                prev_decl)
            else if Option.is_some prev_decl.body then
              compile_error "Duplicate function definition"
                (ASTDeclaration (Function decl))
            else if Option.is_none decl.body then (
              (* Duplicate method declaration. Ignore the later one. *)
              decl.index <- Some (-1);
              prev_decl)
            else (
              prev_decl.index <- decl.index;
              (* Make sure the declaration has the default parameters specified
                 in the method declaration (default values cannot be specified
                 in method definition) *)
              if Option.is_some decl.body then decl.params <- prev_decl.params;
              decl)
        | None -> decl);
      super#visit_fundecl decl

    method! visit_declaration decl =
      match decl with
      | Global ds ->
          List.iter ds.vars ~f:(fun g ->
              match Hashtbl.add ctx.globals ~key:g.name ~data:g with
              | `Duplicate ->
                  (* Base ain already has this global - preserve index *)
                  let existing = Hashtbl.find_exn ctx.globals g.name in
                  g.index <- existing.index;
                  Hashtbl.set ctx.globals ~key:g.name ~data:g
              | `Ok ->
                  if not g.is_const then
                    g.index <- Some (Ain.add_global ctx.ain g.name gg_index))
      | GlobalGroup gg ->
          gg_index <- Ain.add_global_group ctx.ain gg.name;
          List.iter gg.vardecls ~f:(fun ds ->
              self#visit_declaration (Global ds));
          gg_index <- -1
      | Function f ->
          (match Util.parse_qualified_name f.name with
          | None, _ -> ()
          | Some qual, name ->
              if Hashtbl.mem ctx.structs qual then (
                f.name <- name;
                f.class_name <- Some qual;
                if not (Hashtbl.mem ctx.functions (mangled_name f)) then
                  compile_error
                    (f.name ^ " is not declared in class " ^ qual)
                    (ASTDeclaration decl))
              else
                (* Try splitting one level higher for property accessors
                   (e.g., Class::Property::get → class=Class, name=Property::get) *)
                match Util.parse_qualified_name qual with
                | Some qual2, prop_name
                  when Hashtbl.mem ctx.structs qual2 ->
                    f.name <- prop_name ^ "::" ^ name;
                    f.class_name <- Some qual2;
                    if not (Hashtbl.mem ctx.functions (mangled_name f)) then
                      compile_error
                        (f.name ^ " is not declared in class " ^ qual2)
                        (ASTDeclaration decl)
                | _ -> ());
          self#visit_fundecl f
      | FuncTypeDef f -> (
          match Hashtbl.add ctx.functypes ~key:f.name ~data:f with
          | `Duplicate ->
              let existing = Hashtbl.find_exn ctx.functypes f.name in
              f.index <- existing.index;
              Hashtbl.set ctx.functypes ~key:f.name ~data:f
          | `Ok -> f.index <- Some (Ain.add_functype ctx.ain f.name).index)
      | DelegateDef f -> (
          match Hashtbl.add ctx.delegates ~key:f.name ~data:f with
          | `Duplicate ->
              let existing = Hashtbl.find_exn ctx.delegates f.name in
              f.index <- existing.index;
              Hashtbl.set ctx.delegates ~key:f.name ~data:f
          | `Ok -> f.index <- Some (Ain.add_delegate ctx.ain f.name).index)
      | StructDef s -> (
          let unqualified_struct_name =
            snd (Util.parse_qualified_name s.name)
          in
          let ain_s = Ain.add_struct ctx.ain s.name in
          let jaf_s = new_jaf_struct s.name s.loc ain_s.index in
          let next_index = ref 0 in
          let in_private = ref s.is_class in
          let visit_decl = function
            | AccessSpecifier Public -> in_private := false
            | AccessSpecifier Private -> in_private := true
            | Constructor f ->
                if not (String.equal f.name unqualified_struct_name) then
                  compile_error "constructor name doesn't match struct name"
                    (ASTDeclaration (Function f));
                f.class_name <- Some s.name;
                f.class_index <- Some ain_s.index;
                f.is_private <- !in_private;
                self#visit_fundecl f
            | Destructor f ->
                if not (String.equal f.name ("~" ^ unqualified_struct_name))
                then
                  compile_error "destructor name doesn't match struct name"
                    (ASTDeclaration (Function f));
                f.class_name <- Some s.name;
                f.class_index <- Some ain_s.index;
                f.is_private <- !in_private;
                self#visit_fundecl f
            | Method f ->
                f.class_name <- Some s.name;
                f.class_index <- Some ain_s.index;
                f.is_private <- !in_private;
                self#visit_fundecl f
            | MemberDecl ds ->
                List.iter ds.vars ~f:(fun v ->
                    v.is_private <- !in_private;
                    if not v.is_const then (
                      v.index <- Some !next_index;
                      next_index :=
                        !next_index
                        + if is_ref_scalar v.type_spec.ty then 2 else 1);
                    match Hashtbl.add jaf_s.members ~key:v.name ~data:v with
                    | `Duplicate ->
                        compile_error "duplicate member variable declaration"
                          (ASTVariable v)
                    | `Ok -> ())
          in
          List.iter s.decls ~f:visit_decl;
          match Hashtbl.add ctx.structs ~key:s.name ~data:jaf_s with
          | `Duplicate ->
              (* Base ain already has this struct - update rather than error *)
              Hashtbl.set ctx.structs ~key:s.name ~data:jaf_s
          | `Ok -> ())
      | Enum _ ->
          compile_error "enum types not yet supported" (ASTDeclaration decl)
  end

let register_type_declarations ctx decls =
  (new type_declare_visitor ctx)#visit_toplevel decls

(*
 * AST pass to resolve HLL-specific type aliases.
 *)
class hll_type_resolve_visitor ctx =
  object
    inherit ivisitor ctx

    method! visit_type_specifier ts =
      match ts.ty with
      | Unresolved "intp" -> ts.ty <- Ref Int
      | Unresolved "floatp" -> ts.ty <- Ref Float
      | Unresolved "stringp" -> ts.ty <- Ref String
      | Unresolved "boolp" -> ts.ty <- Ref Bool
      | _ -> ()
  end

let resolve_hll_types ctx decls =
  (new hll_type_resolve_visitor ctx)#visit_toplevel decls

(*
 * AST pass to resolve user-defined types (struct/enum/function types).
 *)
class type_resolve_visitor ctx =
  object (self)
    inherit ivisitor ctx as super

    method resolve_type name node =
      match Hashtbl.find ctx.structs name with
      | Some s -> Struct (name, s.index)
      | None -> (
          match Hashtbl.find ctx.functypes name with
          | Some ft -> FuncType (Some (name, Option.value_exn ft.index))
          | None -> (
              match Hashtbl.find ctx.delegates name with
              | Some dg -> Delegate (Some (name, Option.value_exn dg.index))
              | None -> (
                  match name with
                  | "IMainSystem" -> IMainSystem
                  | _ -> compile_error ("Undefined type: " ^ name) node)))

    method! visit_type_specifier ts =
      let rec resolve t =
        match t with
        | Unresolved t -> self#resolve_type t (ASTType ts)
        | Ref t -> Ref (resolve t)
        | Array t -> Array (resolve t)
        | Wrap t -> Wrap (resolve t)
        | _ -> t
      in
      ts.ty <- resolve ts.ty

    method! visit_fundecl decl =
      (if decl.is_lambda then
         match self#env#current_class with
         | Some (Struct (name, index)) ->
             decl.class_name <- Some name;
             decl.class_index <- Some index
         | _ -> ());
      super#visit_fundecl decl

    method! visit_declaration decl =
      (match decl with
      | Function f -> (
          match f.class_name with
          | Some name ->
              f.class_index <- Some (Hashtbl.find_exn ctx.structs name).index
          | _ -> ())
      | FuncTypeDef _ | DelegateDef _ | Global _ | GlobalGroup _ | StructDef _
        ->
          ()
      | Enum _ ->
          compile_error "enum types not yet supported" (ASTDeclaration decl));
      super#visit_declaration decl
  end

let resolve_types ctx decls =
  (new type_resolve_visitor ctx)#visit_toplevel decls

(*
 * AST pass over top-level declarations to define function/struct types.
 *)
class type_define_visitor ctx =
  object (self)
    inherit ivisitor ctx as super

    method! visit_fundecl f =
      super#visit_fundecl f;
      if f.is_lambda then
        let obj =
          Ain.get_function_by_index ctx.ain (Option.value_exn f.index)
        in
        obj |> jaf_to_ain_function f |> Ain.write_function ctx.ain

    method! visit_declaration decl =
      super#visit_declaration decl;
      match decl with
      | Global ds ->
          List.iter ds.vars ~f:(fun g ->
              if not g.is_const then
                Ain.set_global_type ctx.ain g.name
                  (jaf_to_ain_type g.type_spec.ty))
      | GlobalGroup gg ->
          List.iter gg.vardecls ~f:(fun ds ->
              self#visit_declaration (Global ds))
      | Function f ->
          let obj =
            Ain.get_function_by_index ctx.ain (Option.value_exn f.index)
          in
          obj |> jaf_to_ain_function f |> Ain.write_function ctx.ain
      | FuncTypeDef f -> jaf_to_ain_functype f |> Ain.write_functype ctx.ain
      | DelegateDef f -> jaf_to_ain_functype f |> Ain.write_delegate ctx.ain
      | StructDef s -> (
          (* check for undefined methods - skip property accessors (::get/::set/::add/::remove) *)
          let is_property_accessor name =
            String.is_suffix name ~suffix:"::get"
            || String.is_suffix name ~suffix:"::set"
            || String.is_suffix name ~suffix:"::add"
            || String.is_suffix name ~suffix:"::remove"
          in
          List.iter s.decls ~f:(function
            | Method f | Constructor f | Destructor f ->
                if Option.is_none f.index && not (is_property_accessor f.name)
                then
                  if Ain.version ctx.ain >= 11 then
                    (* v11: skip undefined methods - they may be property accessors
                       or methods from emptied files *)
                    ()
                  else
                    compile_error
                      (Printf.sprintf "No definition of %s::%s found" s.name
                         f.name)
                      (ASTDeclaration (Function f))
            | _ -> ());
          match Ain.get_struct ctx.ain s.name with
          | Some obj -> obj |> jaf_to_ain_struct s |> Ain.write_struct ctx.ain
          | None -> compiler_bug "undefined struct" (Some (ASTDeclaration decl))
          )
      | Enum _ ->
          compile_error "Enum types not yet supported" (ASTDeclaration decl)
  end

let define_types ctx decls = (new type_define_visitor ctx)#visit_toplevel decls

let check_builtin_library builtin_type =
  (* All functions in HLL for a built-in type T must have a first argument of
     type ref T. *)
  List.iter ~f:(fun func ->
      match func.params with
      | [] ->
          compile_error "builtin HLL function must have at least one parameter"
            (ASTDeclaration (Function func))
      | param :: _ ->
          if not (Poly.equal param.type_spec.ty (Ref builtin_type)) then
            compile_error
              (Printf.sprintf "first parameter must be of type ref %s"
                 (jaf_type_to_string builtin_type))
              (ASTVariable param))

let define_library ctx decls hll_name import_name =
  let functions =
    List.map decls ~f:(function
      | Function f -> f
      | decl ->
          compiler_bug "unexpected declaration in .hll file"
            (Some (ASTDeclaration decl)))
  in
  (if ctx.version >= 800 then
     match import_name with
     | "Int" -> check_builtin_library Int functions
     | "Float" -> check_builtin_library Float functions
     | "String" -> check_builtin_library String functions
     | "Array" -> check_builtin_library (Array HLLParam) functions
     | "Delegate" -> check_builtin_library (Delegate None) functions
     | _ -> ());
  Ain.write_library ctx.ain
    {
      (Ain.add_library ctx.ain hll_name) with
      functions = List.map ~f:jaf_to_ain_hll_function functions;
    };
  let functions =
    let tbl = Hashtbl.create (module String) in
    List.iter functions ~f:(fun (d : fundecl) ->
        (* Allow duplicate HLL function names (overloads in v11+).
           Store unique overloads by name#arity key. First one keeps the base name. *)
        let key = d.name in
        if Hashtbl.mem tbl key then (
          let arity = List.length d.params in
          let rec find_free suffix =
            let k =
              if suffix = 0 then Printf.sprintf "%s#%d" d.name arity
              else Printf.sprintf "%s#%d_%d" d.name arity suffix
            in
            if Hashtbl.mem tbl k then find_free (suffix + 1)
            else k
          in
          Hashtbl.set tbl ~key:(find_free 0) ~data:d)
        else Hashtbl.set tbl ~key ~data:d);
    tbl
  in
  Hashtbl.set ctx.libraries ~key:import_name ~data:{ hll_name; functions }
